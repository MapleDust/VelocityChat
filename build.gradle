plugins {
    id 'java'
    id 'eclipse'
    id "org.jetbrains.gradle.plugin.idea-ext" version "1.0.1"
}

group = properties.get('plugin_group')
version = properties.get('plugin_version')
def plugin_name = properties.get('plugin_name')
def plugin_id = properties.get('plugin_id')

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'file:/G:/qu-an/Dev/Maven/repo'
    }
    maven {
        name = 'papermc-repo'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    maven {
        url 'https://gitee.com/virtual-qu-an/qu-an-maven/raw/master'
    }
}

dependencies {
    compileOnly 'com.velocitypowered:velocity-api:3.2.0-SNAPSHOT'
    annotationProcessor 'com.velocitypowered:velocity-api:3.2.0-SNAPSHOT'
    compileOnly 'com.electronwill.night-config:toml:3.6.6'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
    compileOnly 'org.projectlombok:lombok:1.18.24'
    implementation "fun.qu-an.lib.basic:Qu_anBasicLib:1.3.0"
    implementation "fun.qu-an.lib.minecraft.vanilla:Qu_anVanillaLib:1.3.0"
    implementation "fun.qu-an.minecraft.velocity.api:Qu_anVelocityApi:1.3.5"
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

// 打包时包含依赖
jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    exclude()
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

def templateSource = file('src/main/templates')
def templateDest = layout.buildDirectory.dir('generated/sources/templates')
def generateTemplates = tasks.register('generateTemplates', Copy) { task ->
    def props = [
            'version': project.version,
            'plugin_id': plugin_id,
            'plugin_name': plugin_name
    ]
    task.inputs.properties props

    task.from templateSource
    task.into templateDest
    task.expand props
}

sourceSets.main.java.srcDir(generateTemplates.map { it.outputs })

rootProject.idea.project.settings.taskTriggers.afterSync generateTemplates
project.eclipse.synchronizationTasks(generateTemplates)
